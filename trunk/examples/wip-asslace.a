
!macro basic_header {
	!byte $b, $08, $EF, $00, $9E, $32, $30, $36,$31, $00, $00, $00 
}

!to "asslace.png.prg", cbm    ; set output file and format
	*= $0801		; Start at C64 BASIC start
	+basic_header		; Call program header macro
    
	lda #$7f
	sta $dc0d      ; no timer IRQs
	lda $dc0d      ; clear timer IRQ flags

	lda	$d0
	lda     $D016 ;enable multicolor
	ora     #$10
	sta     $D016

	lda     #$BB ;enable bitmap mode
	sta     $D011

	lda     #$08 ; video matrix = 8000, bitmap base = a000
	sta     $D018

    lda     #0
	sta     $d020
	lda     #0
	sta     $d021

	; ---------------- setup sprites - start

	lda	#%01111111
	sta	$d015 ;enable all
	sta	$d01d ;2x horiz

	lda	#%11100000
	sta	$d010 ;xpos msb
	
	;sprite ptrs
	ldx	#$6
.ptrloop
	lda	#$10
	sta	$43f8, x
	sta	$83f8, x
	lda	#0
	sta	$d027, x
	dex
	bpl	.ptrloop

	; ---------------- setup sprites - end

	ldx	#0
.memcpy
	lda	$2000, x
	sta	$d800, x
	lda	$2100, x
	sta	$d900, x
	lda	$2200, x
	sta	$da00, x
	lda	$2300, x
	sta	$db00, x
	dex
	bne	.memcpy

.loop
    lda $d012
    cmp #$ff
    bne .loop

	lda     #$16 ;vic base = $4000
	sta     $DD00

	jsr	spriteswap

.loop2
    lda $d012
    cmp #$ff
    bne .loop2

	lda     #$15 ;vic base = $8000
	sta     $DD00

	jsr	spriteswap

    jmp .loop


!macro do_swap .line, .xoffset {
.loop
    lda $d012
    cmp #.line
    bne .loop

!for sprite, 7 {
	sta $d001 + (sprite-1) * 2	
}

!for sprite, 7 {
	lda	#($18 + .xoffset + (48 * (sprite-1))) AND $ff ;xpos lsb
	sta	$d000 + (sprite-1) * 2
}
}

*= $1000
spriteswap

+do_swap ($33-1), (-3)
+do_swap ($33-1+21), (-1)
+do_swap ($33-1+21*2), (-3)
+do_swap ($33-1+21*3), (-1)
+do_swap ($33-1+21*4), (-3)
+do_swap ($33-1+21*5), (-1);badline:(
+do_swap ($33-1+21*6), (-3)
+do_swap ($33-1+21*7), (-1)
+do_swap ($33-1+21*8), (-3);almost badline:(
+do_swap ($33-1+21*9), (-1)
	rts

!macro SpriteLine .v {
	!by .v>>16, (.v>>8)&255, .v&255
}

*= $4000
!bin "asslace.png1-v.bin"

*= $4400

+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
!byte 0	; pad to 64-byte block

*= $6000
!bin "asslace.png1.bin"

*= $8000
!bin "asslace.png2-v.bin"

*= $8400

+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
+SpriteLine %.#.#.#.#.#.#.#.#.#.#.#.#
+SpriteLine %#.#.#.#.#.#.#.#.#.#.#.#.
!byte 0	; pad to 64-byte block

*= $a000
!bin "asslace.png2.bin"

*= $2000
!bin "asslace.png-c.bin"

